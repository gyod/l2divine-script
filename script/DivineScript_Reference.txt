+------------------------+
| DivineScript Reference |
+------------------------+

Version:	2.5
Last updated:	September 5, 2014
Author:		Dev, Zariche

+-------------------+
| Table of Contents |
+-------------------+
Section 1: The Basics
- Writing a Script
- Code Comments
- Compare Operator
- Syntax
-- Case sensitive
-- One command per line
-- Complete start and end scope
-- Represent string with ""
- Global Property
- Including other script
- Encrypt script file
Section 2: Commands
- Action
- ActionSorrow
- AddHuntingArea
- AddRectHuntingArea
- AddItem
- BattleStart
- BattleStop
- Buy
- CancelAttackTarget
- CharBuffLevel
- CharBuffTime
- CheckNPCRange
- ClearHuntingArea
- ClearItemList
- ClearMonsterList
- CloseDialog
- CloseWindow
- Confirm
- CounterTargetPlayer
- CountItem
- CountNPCInRange
- CountQuestDefeatNpc
- CraftItem
- CrystalizeItem	
- Delay
- DestroyItem
- DialogContain
- Dismiss
- DropItem
- EnchantItem
- EnchantLevel
- Exit
- FishingStart
- FishingStop
- FollowSelectTarget
- ForceAttack
- ForceAttackTarget
- FoundEnemyInRange
- FoundKarmaPlayer
- FoundPlayerInRange
- If
- Include
- Invite
- IsMember
- ItemInRange
- Jump
- Label
- LearnSkill
- LeaveParty
- LocInRange
- Logout
- MoveTo
- MoveToTarget
- Msg
- NoMobInHuntingArea
- NpcDialog
- NPCInRange
- PetAction
- Pickup
- PlayerBuffLevel
- PlayerBuffTime
- PlayerInRange
- PlaySound
- QuestAccepted
- Recommend
- Return
- ReturnToVillage
- ReturnToClanHall
- SelectDialog
- SelectKarmaPlayer
- SelectNearestEnemy
- SelectNearestMob
- SelectNearestPlayer
- SelectPlayerByClass
- SelectTarget
- Sell
- SetBattleArea	
- SetRestArea
- SetPickup
- SetAttack
- Sit
- Stand
- SocialAction
- TargetInRange
- TradeStart
- TradeStop
- UseItem
- UseSkill
- While
Section 3: Reference ID

+------------------+
| Section 1: Basic |
+------------------+

Welcome to DivineScript Reference, a comprehensive listing of DivineScript 
command and also includes explanations and examples.

+------------------+
| Writing a Script |
+------------------+

The easiest way to write a simple script is with a text editor. So, using the 
text editor of your choice, create a text file with the following text.
DivineScript are case sensitive, so if you type the code in yourself, 
pay particular attention to the capitalization. 

--- Example ---
Msg(Console, "Hello L2Divine")

+---------------+
| Code Comments |
+---------------+
Double slashes or // is code comments. It's placed in source files to describe 
what is happening in the code to comment-out lines of code to isolate the source 
of execution. 

+------------------+
| Compare Operator |
+------------------+
You can use operators to compare value in If and While command.
DivineScript supported for operator below
==	Equal
!=	Not equal
>	Greater than
<	Lower than
>=	Equal or greater than
<=	Equal or lower than
!	Not
&	And (Bitwise)

--- Example ---

// Use potion when HP fall below 50%
If (Char.HP% < 50)
{
	UseItem("Potion")
}

// Check target abnormal status
If (Target.AbnormalStatus & 7)
{
	Msg(Console, "Stun!")
}

+--------+
| Syntax |
+--------+

--- Case sensitive ---

Valid syntax:	Msg(Console, "Hello L2Divine")

Invalid Syntax:	MSG(Console, "Hello L2Divine")
		mSG(Console, "Hello L2Divine")
		
--- One command per line ---

Valid syntax:	MoveTo(115968, -174345, -760)		
		if (CountItem("Iron Ore") > 0)
		{
		}

Invalid Syntax:	MoveTo(115968, -174345, -760) Delay(3000)
		if (CountItem("Iron Ore") > 0) {
		}							

--- Complete start and end scope ---
	
Valid syntax:	while (Char.HP > 500)			
		{
		}

Invalid Syntax:	while (Char.HP > 500)
		}

--- Represent string with "" ---
	
Valid syntax:	Msg(Console, "Hello L2Divine")

Invalid Syntax:	Msg(Console, Hello L2Divine)

+-----------------+
| Global Property |
+-----------------+
 
You can use following properties to represent your character and target status :-

Char.HP			Character HP
Char.HP%		Character HP percentage
Char.MP			Character MP
Char.MP%		Character MP percentage
Char.CP			Character CP
Char.CP%		Character CP percentage
Char.Soul		Character soul (Kamael only)
Char.LV			Character level
Char.Load%		Character weight percentage
Char.InParty		Character join party status
Char.Vitality		Character vitality
Char.Race		Character race
Char.Class		Character class
Char.EXP%		Character percenrage of current level	
Char.IsSelectTarget	Character is selected target
Char.IsTargetByPlayer	Character is targeted by other player
Char.IsAttackByPlayer	Character is under attack by other player
Target.HP		Target HP
Target.HP%		Target HP percentage
Target.MP		Target MP
Target.MP%		Target MP percentage
Target.Range		Range between character and target
Target.IsDead		Target alive status (Alive/Dead)
Target.AbnormalStatus	Target abnormal status
Target.IsPlayer		Target is player
Target.IsMob		Target is monster
Target.IsNPC		Target is none player character
Target.IsTeam		Target is your team
Target.IsEnemy		Target is enemy
Target.IsClanWar	Target is member of your clan war

+------------------------+
| Including other script |
+------------------------+

You can insert the content of one script file into another script file, with the Include command.

--- Example main.txt ---
Include("mylib.txt")
Msg(Console, "Hello 1")
Msg(Console, "Hello 2")

--- Example mylib.txt ---
Msg(Console, "MyLib -> Hello 1")
Msg(Console, "MyLib -> Hello 2")

Note! The specified script must be in the same directory with the file that uses the command.

+------------------------+
| Encrypt script file |
+------------------------+

You can encrypt your script file to make it unable to read and edit by 
text editor program but still can run with L2Divine.

To enable this feature, do following step:
1. Open L2Divine.ini
2. Add "EnableScriptEncryption=1" under the "Option" section
3. Save and restart L2Divine

-- Example --
[Option]
EnableScriptEncryption=1

The script file will automatic encrypt when it run.
The encrypted script file will save in same folder with the running script
but the file extension will change to .dys instead. 

+--------------------+
| Section 2: Command |
+--------------------+

Action(Action Id)
	Parameter	Action id (See@ActionID)
	Return		None
	Propose		Do action by specified action id
			Note: Some action required parameter(s) that not supported by the script engine
			and you will be possible to disconnect from server. 
			Please use it as your own risk.

ActionSorrow()
	Parameter	None
	Return		None
	Propose		Action sorrow 
			Note: This is alias for Action(35)
	
AddHuntingArea(x, y, z, radius)
AddHuntingArea(radius)
	Parameter	Location (x,y,z) and radius, radius (use current location)
	Return		None
	Propose		Add circle hunting area to current setting
	Example		AddHuntingArea(115968, -174345, -760, 2500)

AddRectHuntingArea(x, y, z, radius)
AddRectHuntingArea(radius)
	Parameter	Location (x,y,z) and radius, radius (use current location)
	Return		None
	Propose		Add rectangle hunting area to current setting
	Example		AddRectHuntingArea(115968, -174345, -760, 2500)

AddItem(Item ID, Count)
AddItem(Item Name, Count)
	Parameter	Item ID or Item Name (Use * for all item in inv), Quantity (Use * for all)
	Return		None
	Propose		Add item to buy, sell, withdraw and deposit list of 
			opening trade dialog.
	Example		AddItem("Scroll of Escape", 1)
			AddItem(17, *)
BattleStart()	
	Parameter	None
	Return		None
	Propose		Activate character in battle mode (Same as press 
			"Battle Start" button)
	
BattleStop()	
	Parameter	None
	Return		None
	Propose		Deactivate character from battle mode (Same as press 
			"Battle Stop" button)
	
Buy()	
	Parameter	None
	Return		None
	Propose		Change NPC trade mode to buy (Gracia plus only)

CancelAttackTarget()
	Parameter	None
	Return		None
	Propose		Cancel action (melee attack/skill) on current target

CharBuffLevel(Skill ID)
CharBuffLevel(Skill Name)
	Parameter	Skill ID or Skill Name
	Return		Level of skill effected
	Propose		Get level of skill effected of character (Use with If and While command)

CharBuffTime(Skill ID)
CharBuffTime(Skill Name)
	Parameter	Skill ID or Skill Name
	Return		Remaining time of skill effected
	Propose		Get remaining time of skill effected of character (Use with If and While command)

CheckNPCRange(NPC ID)
	Parameter	NPC/Mob ID
	Return		True/False
	Propose		Check NPC/mob in specified range (Use with If and While command)
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			Start:
			If (CheckNPCRange(33587) < 100)
			{
				Msg(Console, "In range!!!")
			}
			Else
			{
				Msg(Console, "Not in range ...")
			}

			Delay(1000)
			Jump("Start")
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

ClearHuntingArea()	
	Parameter	None
	Return		None
	Propose		Remove all hunting area from current setting
	
ClearItemList()	
	Parameter	None
	Return		None
	Propose		Remove all items in setting list

ClearMonsterList()	
	Parameter	None
	Return		None
	Propose		Remove all monsters in setting list

CloseDialog()
	Parameter	None
	Return		None
	Propose		Close opening NPC dialog

CloseWindow()
	Parameter	None
	Return		None
	Propose		Close all opening window under current game account

Confirm()
	Parameter	None
	Return		None
	Propose		Confirm buy, sell, withdraw and deposit item with NPC
	Tip		Right click on button to copy it content (NEW in L2Divine 11.1.0)
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Talk with Mr.CAT
			NpcDialog(31756)
			Delay(2000)

			// Select list no. 6,1 to buy item
			SelectDialog(6)
			Delay(2000)
			SelectDialog(1)
			Delay(2000)

			// Add Crystal D-Grade x 3 to trade list
			AddItem(1458, 3)

			// Confirm trade
			Confirm()

			// Close openning dialog
			CloseDialog()
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

CounterTargetPlayer()
	Parameter	None
	Return		None
	Propose		Target back to player who targeting your character
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			If (Char.IsTargetByPlayer)
			{
				Msg(Console, "Targeting by player")
				// Target back
				CounterTargetPlayer()
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

CountItem(Item ID)
CountItem(Item Name)
	Parameter	Item ID or Item Name
	Return		Total quantity of item
	Propose		Count item in inventory (Use with If and While command)
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Count no of Soulshot: D Grade
			While (CountItem(1463) < 1000)
			{
				// Do something
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

CountQuestDefeatNpc(NPC ID)
	Parameter	NPC ID
	Return		Number of defeated NPC
	Propose		Count number of defeated NPC in quest (Use with If and While command)
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Count defeated Stalker of quest id 10362 (Certification of the Seeker)
			If (CountQuestDefeatNpc(22992) == 10)
			{
				// Do something
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

CountNPCInRange(NPC ID, x, y, z, radius)
CountNPCInRange(NPC Name, x, y, z, radius)
	Parameter	NPC ID/Name, x, y, z, radius
	Return		Number of NPC in specified area
	Propose		Count number of NPC in specified area (Use with If and While command)
			Note: You can use x,y,z=0 to represent your current location

CraftItem(Receipt ID)
	Parameter	Reciept ID
	Return		None
	Propose		Manufaturing item
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Create Soulshot: D Grade
			CraftItem(20)
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

CrystalizeItem(Item ID)
CrystalizeItem(Item Name)
	Parameter	Item ID or Item name
	Return		None
	Propose		Crystalize item	

Delay(Millisecond)
	Parameter	Time in millisecond
	Return		None
	Propose		Pause script execution

DestroyItem(Item ID, count)
DestroyItem(Item Name, count)
	Parameter	Item ID/Name and count (Use * for all)
	Return		None
	Propose		Destroy item

DialogContain(text)
	Parameter	Text
	Return		True/False
	Propose		Check opening dialog contain whether specific text or not
			(Use with If and While command)
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			If (DialogContain("2.4 km"))
			{
				SelectDialog("2.4 km")
				Return
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Dismiss(Name)
	Parameter	Player name
	Return		None
	Propose		Dismiss player from party

DropItem(Item ID, count)
DropItem(Item Name, count)
	Parameter	Item ID/Name and count (Use * for all)
	Return		None
	Propose		Drop item at current location

EnchantItem(Item ID, Item ID)
	Parameter	Weapon or Armor ID, Enchant Scroll ID
	Return		None
	Propose		Enchant weapon and armor with specific enchant scroll
	Example		See@Enchant Item.txt

EnchantLevel(Item ID)
	Parameter	Item ID
	Return		Level of enchantment
	Propose		Get enchant level of specific item ID (Use with If and While command)		

Exit
	Parameter	None
	Return		None
	Propose		Exit script

FishingStart()
	Parameter	None
	Return		None
	Propose		Active character in fishing mode (Same as press 
			"Fishing Start" button)

FishingStop()
	Parameter	None
	Return		None
	Propose		Deactive character from fishing mode (Same as press 
			"Fishing Stop" button)

FollowSelectTarget(Name)
	Parameter	Name of object in map
	Return		None
	Propose		Target to target of specified object name
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Target to target of player name "dev"
			FollowSelectTarget("dev")
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

ForceAttack(NPC ID)
	Parameter	NPC ID
	Return		None
	Propose		Force attack to NPC
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			Down Level:

			// Attack until die
			While (Char.HP > 0)
			{
				ForceAttack(30545)
				Delay(2000)
			}

			// Return to nearest village
			ReturnToVillage()
			Delay(2000)
			
			// Down level until 25
			If (Char.LV > 25)
			{
				Jump("Down Level")
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

ForceAttackTarget()
	Parameter	None
	Return		None
	Propose		Force melee attack to selected target
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			Start:

			// Check pvp condition every 1 sec
			Delay(1000)
			If (Char.IsAttackByPlayer)
			{
				Msg(Console, "Attack by Player!")
				Jump("PVP")
			}
			Else 
			{
				Jump("Start")
			}

			PVP:

			// Select player
			CounterTargetPlayer()

			// Move to target in range 100
			If (Target.Range > 100)
			{
				MoveToTarget(100)	
			}

			Delay(500)
			ForceAttackTarget()

			// Attack until dead
			If (!Target.IsDead)
			{
				Jump("PVP")		
			}
			Else 
			{
				Jump("Start")
				CancelAttackTarget()
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

ForcePetAction(Action ID)
ForcePetAction(Action Name)
	Parameter	Action ID or Action Name
	Return		None
	Propose		Force use pet action
	Example		ForcePetAction("Attack")

FoundEnemyInRange(Range)
	Parameter	Range from current location
	Return		True/False
	Propose		Check enemy in specified range (Use with If and While command)

FoundKarmaPlayer(Range)
	Parameter	Range from current location
	Return		True/False
	Propose		Check karma player in specified range (Use with If and While command)

FoundPlayerInRange(Range)
	Parameter	Range from current location
	Return		True/False
	Propose		Check other player in specified range (Use with If and While command)
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Check player in range 1000
			If (FoundPlayerInRange(100))
			{
				Msg(Console, "Found!!!")
			}
			Else
			{
				Msg(Console, "Not Found")
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

If () {} Else {}
	Parameter	Global property/function, comparator and value
	Return		True/False
	Propose		Compare your statement
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			If (CountItem(1835) == 0)
			{
				// Select list no. 1 to buy item
				SelectDialog(1)	
			}
			Else
			{
				// Sell list no. 3 to sell item
				SelectDialog(3)
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Include(filename)
	Parameter	filename
	Return		None
	Propose		Include other script file

Invite(Name)
	Parameter	Player name
	Return		None
	Propose		Invite target to join party
	Example		Invite("dev")

IsMember(Name)
	Parameter	Player name
	Return		True/False
	Propose		Check player whether in party or not
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			If (IsMember("dev"))
			{
				Msg(Console, "dev joined party")
			}
			Else
			{
				Msg(Console, "dev not in party")	
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

ItemInRange(Item ID, x, y, z, radius)
	Parameter	Item ID, x, y, z, radius
	Return		True/False
	Propose		Check item in specified area
			(Use with If and While command)
			Note: You can use x,y,z=0 to represent your current location
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Pickup adena
			While (ItemInRange(57,0,0,0,1000))
			{
				Msg(Console, "Picking ...")
				Pickup()
				Delay(2000)
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Jump(Label)
	Parameter	Address label
	Return		None
	Propose		Jump to specified label

Label:
	Parameter	None
	Return		None
	Propose		Define address
	Example		Exit:

LearnSkill(Skill ID)
LearnSkill(Skill Name)
	Parameter	Skill ID or Skill Name
	Return		None
	Propose		Learn skill
	Example		LearnSkill(141)

LeaveParty()
	Parameter	None
	Return		None
	Propose		Leave from party		

LocInRange(x, y, z, radius)
	Parameter	location (x, y, z) and radius
	Return		True/False
	Propose		Check location whether in specified circle area or not
			(Use with If and While command)
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			If (LocInRange(-59569, -56184, -2032, 500))
			{
				Msg(Console, "In range")
			}
			Else
			{
				Msg(Console, "Not in range")
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Logout(Delay, Relogin Delay)
	Parameter	Logout delay time and relogin delay time (In second)
	Return		None
	Propose		Force logout and relogin
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Logout now and reloing in next 10 second	
			Logout(0, 10)
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

MoveTo(x, y, z)
	Parameter	location (x, y, z)
	Return		None
	Propose		Move your character to specified location. Use /loc command
			to get your current location.
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Move to blackwolf spwan location
			MoveTo(116633, -183771, -1568)
			MoveTo(116504, -183755, -1560)
			MoveTo(116548, -184397, -1568)
			MoveTo(116633, -185163, -1744)
			MoveTo(117115, -186064, -2088)
			MoveTo(117678, -186667, -2384)
			MoveTo(118566, -187339, -2776)
			MoveTo(119119, -188103, -3096)
			MoveTo(119674, -188351, -3264)
			MoveTo(120054, -187729, -3304)
			MoveTo(119537, -186438, -3248)	
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

MoveToTarget(range)
	Parameter	Range between character and target
	Return		None
	Propose		Move your character to target location.

Msg(Output, Message)
Msg(Output, Message, Target)
	Parameter	Output and message. Following are output type.
			Console		Represent console message (Internal use in L2Divine)
			General		Represent general message
			Shout		Represent shout message
			Trade		Represent trade message
			Party		Represent party message
			Clan		Represent clan message
			Ally		Represent ally message
			Friend		Represent friend message (Target required)
			Whisper		Represent whisper message (Target required)

	Return		None
	Propose		Send message to target
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			Msg(General, "Hello")
			Msg(Whisper, "Hello", "Dev")
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

NoMobInHuntingArea()
	Parameter	None
	Return		True/False
	Propose		Check the monster in current hunting area
			(Use with If and While command)
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			If (NoMobInHuntingArea())
			{
				Msg(Console, "No Mob")
			}
			Else
			{
				Msg(Console, "Found Mob")
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

NpcDialog(NPC ID)
NpcDialog(NPC Name)
	Parameter	NPC ID or NPC name
	Return		None
	Propose		Open NPC dialog 
	Example		See@Revenge of the Redbonnet.txt

NPCInRange(NPC ID, x, y, z, radius)
NPCInRange(NPC Name, x, y, z, radius)
	Parameter	NPC ID or NPC name, x, y, z, radius
	Return		True/False
	Propose		Check NPC in specified area
			Note: You can use x,y,z=0 to represent your current location

PetAction(Action ID)
PetAction(Action Name)
	Parameter	Action ID or Action Name
	Return		None
	Propose		Use pet action
	Example		PetAction("Attack")

Pickup()
	Parameter	None
	Return		None
	Propose		Pickup 1 nearest item (Only item that belong to you or your team).

PlayerBuffLevel(Name, Skill ID)
	Parameter	Player name, Skill ID
	Return		True/False
	Propose		Check level of buff of specified player	
			(Use with If and While command)
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Check mentor buff
			If (PlayerBuffLevel("Foo",9227) == 1)
			{
				Msg(Console, "BUFF!!!")
			}
			Else
			{
				Msg(Console, "NO BUFF")
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

PlayerBuffTime(Name, Skill ID)
	Parameter	Player name, Skill ID
	Return		True/False
	Propose		Check remaining time of buff of specified player 
			(Use with If and While command)

PlayerInRange(Name, x, y, z, radius)
	Parameter	Player name, x, y, z, radius
	Return		True/False
	Propose		Check player in specified area
			(Use with If and While command)
			Note: You can use x,y,z=0 to represent your current location

PlaySound(Sound File)
	Parameter	Part of sound file
	Return		None
	Propose		Play sound
			(Use with If and While command)
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			PlaySound("C:\temp\L2Divine\sound\Alert.wav")
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Recommend(Player Name)
	Parameter	Player name
	Return		None
	Propose		Give recommend to player

QuestAccepted(Quest Id)
	Parameter	Quest Id
	Return		True/False
	Propose		Check the quest is accepted or not 
			(Use with If and While command)
			If (QuestAccepted(10324))
			{
				// Do something ...
			}

Return
	Parameter	None
	Return		None
	Propose		Return to next command of called Jump
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			Jump("Start")

			MyProc1:
			Msg(Console, "Proc1 Command 1")
			Return

			MyProc2:
			Msg(Console, "Proc2 Command 1")
			Msg(Console, "Proc2 Command 2")
			Return

			Start:
			Msg(Console, "Command 1")
			Jump("MyProc1")
			Msg(Console, "Command 2")
			Jump("MyProc2")
			Msg(Console, "Command 3")
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

ReturnToVillage()
	Parameter	None
	Return		None
	Propose		Return to nearest village after die
	Example		See@Down Level.txt

ReturnToClanHall()
	Parameter	None
	Return		None
	Propose		Return to clan hall after die

SelectDialog(Link Name)
SelectDialog(Link Index)
	Parameter	Link name or Link index
	Return		None
	Propose		Select link from opening NPC dialog

SelectKarmaPlayer()
	Parameter	None
	Return		None
	Propose		Select nearest karma player in range 10000 of your current location

SelectNearestEnemy()
	Parameter	None
	Return		None
	Propose		Select nearest enemy in range 10000 of your current location

SelectNearestMob()
	Parameter	None
	Return		None
	Propose		Select nearest mob in range 10000 of your current location
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Set battle area
			SetBattleArea(135470, -47998, -3096, 3000)
			
			// Target to nearest mob
			SelectNearestMob()

			// Attack it!
			While (!Target.IsDead)
			{
				Delay(500)
				ForceAttackTarget()
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

SelectNearestPlayer()
	Parameter	None
	Return		None
	Propose		Select nearest player in range 10000 of your current location

SelectPlayerByClass(Class ID)
	Parameter	Class ID
	Return		None
	Propose		Select nearest player by class in range 10000 of your current location
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Select Wynn Summoner
			SelectPlayerByClass(145)
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

SelectTarget(Name)
SelectTarget(Name, Title)
SelectTarget(NPC ID)
	Parameter	Player name, NPC ID or name or name & title
	Return		None
	Propose		Target on Player/NPC/Mob (Synonym "Self" for your character)
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Buff myself
			SelectTarget("Self")
			UseSkill(1059)
			UseSkill(1068)
			UseSkill(1077)
			UseSkill(1040)
			UseSkill(1204)
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Sell()	
	Parameter	None
	Return		None
	Propose		Change NPC trade mode to sell (Gracia plus only)

SetBattleArea(x, y, z, radius)
SetBattleArea(radius)
	Parameter	Location and radius, radius (use current location)
	Return		None
	Propose		Set battle area to current setting
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			SetBattleArea(115968, -174345, -760, 2500)

			// Set battle area from current location with range 1000
			SetBattleArea(1000)
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	
SetRestArea(x, y, z)
	Parameter	Location
	Return		None
	Propose		Set rest area to current setting 
	Example		SetRestArea(115968, -174345, -760)

SetPickup(Item Id, true/false)
	Parameter	Item Id, true/false
	Return		None
	Propose		Set to pick specified item or not 
	Example		SetPickup(57, false) // Set to not pick adena

SetAttack(NPC Id, true/false)
	Parameter	NPC Id, true/false
	Return		None
	Propose		Set to attack specified monster or not 
	Example		SetAttack(23113, true) // Set to attack crawler

Sit()
	Parameter	None
	Return		None
	Propose		Set character to sit
			Note: This is alias for Action(0). Toggle between Sit/Stand

Stand()
	Parameter	None
	Return		None
	Propose		Set character to stand
			Note: This is alias for Action(0). Toggle between Sit/Stand

SocialAction(Action ID)
	Parameter	Action ID (see@ActionID)
	Return		None
	Propose		Use social action command
			Note: This command was deprecated with Action() since L2Divine 10.6.10

TargetInRange(Name)
TargetInRange(NPC ID)
	Parameter	Player name, NPC ID or name
	Return		True/False
	Propose		Check player/NPC whether in visible range or not
			(Use with If and While command)
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			If (TargetInRange(32378)) 
			{
				Msg(Console, "Found")
			}
			Else 
			{
				Msg(Console, "Not Found")
			}
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

TradeStart()
	Parameter	None
	Return		None
	Propose		Activate character in trading mode (Same as press "Trade Start" button)
	Example		See@Craft Item.txt

TradeStop()
	Parameter	None
	Return		None
	Propose		Deactivate character from trading mode (Same as press "Trade Stop" button)

UseItem(Item ID)
UseItem(Item Name)
	Parameter	Item ID or Item name
	Return		None
	Propose		Use item
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			UseItem(1830)
			UseItem("Scroll of Escape")
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

UseSkill(Skill ID)
UseSkill(Skill Name)
UseSkill(Skill ID, Target)
UseSkill(Skill Name, Target)
	Parameter	Skill ID or Skill Name and target
	Return		None
	Propose		Use skill on target
	Example		See@Faverites_Buff.txt


While() {}
	Parameter	Global property/function, comparator and value
	Return		True/False
	Propose		Loop until result of your statement become to false
	Example		~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
			// Start battle
			Battle:
			BattleStart()

			// Fight until Black Wolf Pelt == 40
			While (CountItem(1482) < 40)
			{
				Delay(10000)
			}

			// Stop battle
			BattleStop()
			~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

+-------------------------+
| Section 3: Reference ID |
+-------------------------+

+-----------+
| Char.Race |
+-----------+
0=Human
1=Elf
2=Darkelf
3=Orc
4=Dwarf
5=Kamael

+------------+
| Char.Class |
+------------+
0=Fighter
1=Warrior
2=Gladiator
3=Warlord
4=Knight
5=Paladin
6=Dark Avenger
7=Rogue
8=Treasure Hunter
9=Hawkeye
10=Mage
11=Wizard
12=Sorceror
13=Necromancer
14=Warlock
15=Cleric
16=Bishop
17=Prophet
18=Elven Fighter
19=Elven Knight
20=Temple Knight
21=Sword Singer
22=Elven Scout
23=Plains Walker
24=Silver Ranger
25=Elven Mage
26=Elven Wizard
27=Spellsinger
28=Elemental Summoner
29=Oracle
30=Elder
31=Dark Fighter
32=Palus Knight
33=Shillien Knight
34=Bladedancer
35=Assassin
36=Abyss Walker
37=Phantom Ranger
38=Dark Mage
39=Dark Wizard
40=Spellhowler
41=Phantom Summoner
42=Shillien Oracle
43=Shillen Elder
44=Orc Fighter
45=Orc Raider
46=Destroyer
47=Orc Monk
48=Tyrant
49=Orc Mage
50=Orc Shaman
51=Overlord
52=Warcryer
53=Dwarven Fighter
54=Scavenger
55=Bounty Hunter
56=Artisan
57=Warsmith
88=Duelist
89=Dreadnought
90=Phoenix Knight
91=Hell Knight
92=Sagittarius
93=Adventurer
94=Archmage
95=Soultaker
96=Arcana Lord
97=Cardinal
98=Hierophant
99=Eva Templar
100=Sword Muse
101=Wind Rider
102=Moonlight Sentinel
103=Mystic Muse
104=Elemental Master
105=Eva Saint
106=Shillien Templar
107=Spectral Dancer
108=Ghost Hunter
109=Ghost Sentinel
110=Storm Screamer
111=Spectral Master
112=Shillien Saint
113=Titan
114=Grand Khauatari
115=Dominator
116=Doom Cryer
117=Fortune Seeker
118=Maestro
123=Kamael Soldier
124=Kamael Soldier
125=Dragoon
126=Warder
127=Berserker
128=Soul Breaker
129=Soul Breaker
130=Arbalester
131=Doom Bringer
132=Soul Hound
133=Soul Hound
134=Trickster
135=Inspector
136=Judicator
139=Sigel Knight
140=Tyrr Warrior
141=Othell Rogue
142=Yul Archer
143=Feoh Wizard
144=Iss Enchanter
145=Wynn Summoner
146=Aeore Healer
148=Sigel Phoenix Knight
149=Sigel Hell Knight
150=Sigel Eva's Templar
151=Sigel Shillien Templar
152=Tyrr Duelist
153=Tyrr Dreadnought
154=Tyrr Titan
155=Tyrr Grand Khavatari
156=156Tyrr Maestro
157=Tyrr Doombringer
158=Othell Adventurer
159=Othel Wind Rider
160=Othell Ghost Hunter
161=Othell Fortune Seeker
162=Yul Sagittarius
163=Yul Moonlight Sentinel
164=Yul Ghost Sentinel
165=Yul Trickster
166=Feoh Archmage
167=Feoh Soultaker
168=Feoh Mystic Muse
169=Feoh Storm Screamer
170=Feoh Soul Hound
171=Iss Hierophant
172=Iss Sword Muse
173=Iss Spectral Dancer
174=Iss Dominator
175=Iss Doomcryer
176=Wynn Arcana Lord
177=Wynn Elemental Master
178=Wynn Spectral Master
179=Aeore Cardinal
180=Aeore Eva's Saint
181=Aeore Shillien Saint
182=Ertheia Fighter
183=Ertheia Wizard
184=Marauder
185=Cloud Breaker
186=Ripper
187=Stratomancer
188=Eviscerator
189=Sayha's Seer

+-----------------------+
| Target.AbnormalStatus |
+-----------------------+
1=Bleed
2=Poison
7=Stun
8=Sleep
9=Mute
10=Root
11=Hold
12=Hold
14=Big Head
15=Flame
18=Float & Root
19=Dance & Stun
20=Fire & Root & Stun
21=Stealth
22=Imprisioning
23=Imprisioning
24=Magic Circle
28=Ultimate Defend

+----------+
| ActionID |
+----------+
0=Sit/Stand
1=Walk/Run
2=Attack
3=Exchange
4=Next Target
5=Pick Up (Not work)
6=Assist
7=Invite
8=Leave Party
9=Dismiss Party Member
10=Private Store - Sell
11=Party Matching
12=Greeting
13=Victory
14=Advance
15=Change Movement Mode
16=Attack
17=Stop
18=Pickup (Pet)
19=Unsummon Pet
20=Special skill
21=Change Movement Mode
22=Attack
23=Stop
24=Yes
25=No
26=Bow
27=Special skill
28=Private Store - Buy
29=Unaware
30=Social Waiting
31=Laugh
32=Switch Mode
33=Applaud
34=Dance
35=Sorrow
36=Toxic Smoke
37=Dwarven Workshop
38=Mount/Dismount
39=Parasite Burst
40=Recommend
41=Wild Cannon
42=Self Damage Shield
43=Hydro Screw
44=Boom Attack
45=Master Recharge
46=Mega Storm Strike
47=Steal Blood
48=Mech. Cannon
49=Wield Temper
50=Change Party Leader
51=General Workshop
52=Unsummon
53=Move
54=Move
55=Start/End Recording Replay
56=Command Channel Invite
57=Find Store
58=Duel
59=Withdraw
60=Party Duel
61=Package Sale
62=Charm
63=Mini-Game
64=My Teleports
65=Bot Report Button
66=Shyness
67=Steer
68=Cancel Control
69=Destination Map
70=Exit Airship
71=Exchange Bows
72=High Five
73=Couple Dance
74=Turn On/Off the Status Information
76=Invite Friends
77=Start/End Recording
78=Use Token 1
79=Use Token 2
80=Use Token 3
81=Use Token 4
82=Token 1 Targeting
83=Token 2 Targeting
84=Token 3 Targeting
85=Token 4 Targeting
86=Start/Stop Automatic Party Search
87=Propose
88=Provoke
89=Show Off
90=Instanced Zone
1000=Siege Hammer
1001=Ultimate Bombastic Buster
1002=Hostile Feeling
1003=Wild Stun
1004=Wild Defense
1005=Bright Burst
1006=Bright Heal
1007=Blessing of Queen
1008=Gift of Queen
1009=Cure of Queen
1010=Blessing of Seraphim
1011=Gift of Seraphim
1012=Cure of Seraphim
1013=Curse of Shade
1014=Mass Curse of Shade
1015=Shade Sacrifice
1016=Cursed Blow
1017=Cursed Strike
1018=Cursed Drain Energy
1019=Kat the Cat Skill 2
1020=Mew the Cat Skill 2
1021=Kai the Cat Skill 2
1022=Boxer the Unicorn Skill 2
1023=Mirage the Unicorn Skill 2
1024=Merrow the Unicorn Skill 2
1025=Shadow Skill 1
1026=Shadow Skill 2
1027=Silhouette Skill 2
1028=Reanimated Man Skill 1
1029=Reanimated Man Skill 2
1030=Corrupted Man Skill 2
1031=Slash
1032=Spin Slash
1033=Grip of the Cat
1034=Whiplash
1035=Tidal Wave
1036=Corpse Burst
1037=Dicing Death
1038=Dark Curse
1039=Cannon Fodder
1040=Big Bang
1041=Bite Attack
1042=Maul
1043=Cry of the Wolf
1044=Awakening
1045=Wolf Howl
1046=Strider Roar
1047=Divine Beast Bite
1048=Divine Beast Stun Attack
1049=Divine Beast Fire Breath
1050=Divine Beast Roar
1051=Blessed Body
1052=Blessed Soul
1053=Haste
1054=Acumen
1055=Clarity
1056=Empower
1057=Wild Magic
1058=Death Whisper
1059=Focus
1060=Guidance
1061=Death Blow
1062=Double Attack
1063=Spin Attack
1064=Meteor Shower
1065=Awakening
1066=Thunder Bolt
1067=Flash
1068=Lightning Wave
1069=Flare
1070=Buff Control
1071=Power Striker
1072=Piercing Attack
1073=Whirlwind
1074=Lance Smash
1075=Battle Cry
1076=Power Smash
1077=Energy Burst
1078=Shock Wave
1079=Howl
1080=Phoenix Rush
1081=Phoenix Cleanse
1082=Phoenix Flame Feather
1083=Phoenix Flame Beak
1084=Switch State
1086=Panther Cancel
1087=Panther Dark Claw
1088=Panther Fatal Claw
1089=Tail Strike
1090=Strider Bite
1091=Strider Fear
1092=Strider Dash
1093=Maguen Strike
1094=Maguen Wind Walk
1095=Maguen Power Strike
1096=Elite Maguen Wind Walk
1097=Maguen Return
1098=Maguen Party Return
1099=Attack
1100=Move
1101=Stop
1102=Unsummon
1103=Passive
1104=Defend
1106=Bear Claw
1107=Bear Tumbling
1108=Cougar Bite
1109=Cougar Pounce
1110=Reaper Touch
1111=Reaper Power
1113=Lion Roar
1114=Lion Claw
1115=Lion Dash
1116=Lion Flame
1117=Thunder Flight
1118=Thunder Purity
1120=Thunder Feather
1121=Thunder Sharp Claw
1122=Blessing of Life
1123=Siege Punch
1124=Feline Aggression
1125=Feline Stun
1126=Feline Bite
1127=Feline Pounce
1128=Feline Touch
1129=Feline Power
1130=Unicorn's Aggression
1131=Unicorn's Stun
1132=Unicorn's Bite
1133=Unicorn's Pounce
1134=Unicorn's Touch
1135=Unicorn's Power
1136=Phantom Aggression
1137=Phantom Stun
1138=Phantom Bite
1139=Phantom Pounce
1140=Phantom Touch
1141=Phantom Power
1142=Panther Roar
1143=Panther Rush
5000=Hand of Warmth
5001=Rosy Seduction
5002=Art of Seduction
5003=Thunder Bolt
5004=Flash
5005=Lightning Wave
5006=Buff Control
5007=Piercing Attack
5008=Spin Attack
5009=Impale
5010=Battle Cry
5011=Power Smash
5012=Energy Burst
5013=Shock Wave
5014=Burst
5015=Switch Tactics
5016=Cat the Ranger Boost